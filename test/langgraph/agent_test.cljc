(ns langgraph.agent-test
  (:require [clojure.test :refer [deftest is testing]]
            [langgraph.prebuilt :as prebuilt]
            [langchain.model :as model]
            [langchain.message :as msg]
            [langgraph.graph :as g]
            [langchain.db :as db]
            [langgraph.viz :as viz]))

(def weather-tool
  {:name "get_weather"
   :description "Get current weather for a location"
   :schema {:type "object"
            :properties {:location {:type "string"}}
            :required ["location"]}
   :fn (fn [{:keys [location]}] (str "72F and sunny in " location))})

(deftest react-agent-loop
  (let [m (model/mock-model
           [(msg/ai "" {:tool-calls [{:id "t1" :name "get_weather"
                                      :input {:location "Paris"}}]})
            (msg/ai "It is 72F and sunny in Paris.")])
        agent (prebuilt/create-react-agent {:model m :tools [weather-tool]})
        out (g/invoke agent {:messages [(msg/user "Weather in Paris?")]})
        msgs (:messages out)]
    (is (= [:user :assistant :tool :assistant] (mapv :role msgs)))
    (testing "tool result fed back to the model"
      (is (= "72F and sunny in Paris" (:content (nth msgs 2)))))
    (is (= "It is 72F and sunny in Paris." (:content (msg/last-message msgs))))))

(deftest react-agent-tool-error
  (let [m (model/mock-model
           [(msg/ai "" {:tool-calls [{:id "t1" :name "no_such_tool" :input {}}]})
            (msg/ai "Sorry, I cannot do that.")])
        agent (prebuilt/create-react-agent {:model m :tools [weather-tool]})
        out (g/invoke agent {:messages [(msg/user "hi")]})]
    (is (true? (:error? (nth (:messages out) 2))))))

(deftest viz-smoke
  (let [agent (prebuilt/create-react-agent
               {:model (model/mock-model [(msg/ai "x")]) :tools []})
        mm (viz/mermaid agent)]
    (is (re-find #"flowchart TD" mm))
    (is (re-find #"agent" mm))
    (is (re-find #"tools --> agent" mm))))
